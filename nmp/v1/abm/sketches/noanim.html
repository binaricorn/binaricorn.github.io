<html>
  <head>
    <title>No Animation Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../lib/mouse.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    
    # Sketch linkhit but not using the animator, just mouse event callback
    # Note use of body onload: once(), ditto mouse callback
    # Idea: body onload does not start the animator, instead, advances it
    # manually via the once() method.  Setup also installs once() fore any
    # mouse down/move.  Thus the animator is driven by the mouse, not "clocks"
    u = ABM.Util # shortcut

    class MyModel extends ABM.Model
      setup: -> # called by Model constructor
        @refreshPatches = @refreshTurtles = false
        @mouse = new ABM.Mouse @, => @once()
        
        @turtles.setDefault "shape", "circle"; @turtles.setDefault "size", .5
        p.sprout 1 for p in @patches.nOf 20
        @links.create a, @turtles.other(a).oneOf() for a in @turtles

      step: ->
        for l in @links
          l.color = if @mouseOnLink l then [256,0,0] else [128,128,128]

      mouseOnLink: (l) ->
        d = u.aSum (a.distanceXY @mouse.x, @mouse.y for a in l.bothEnds())
        d - l.length() < 1 / @patches.size

    # Note: do not call start()
    window.model = new MyModel {
      div: "layers",
      size: 25,
      minX: -8,
      maxX: 8,
      minY: -8,
      maxY: 8
    }
    .debug() # Debug: Put Model vars in global name space
    </script>
  </head>
  <body onload="model.once()">
    <div id="layers"></div>
  </body>
</html>


